pragma Singleton
import QtQuick
import Quickshell
import Quickshell.Io

// Wallpaper source for the switcher: runs index.sh (thumbnails + a dominant-hue
// reading for every image and video), buckets each entry by colour, and applies
// a pick through `ryogami wallpaper set` (the Ryogami daemon owns the wallpaper,
// transitions, palette and per-output state now), matching the random keybind.
// Entries come back sorted by colour, neutral last, so the grid reads as a
// rainbow before any filtering.
//
// entry = { type ("image"|"live"), path, name, mtime, thumb, hue, sat, group }.
Singleton {
    id: root

    property var entries: []
    readonly property int count: entries.length
    property bool loading: false

    // index.sh path = RYOKU_SHELL_DIR in dev, else the installed quickshell tree
    // (the plugins idiom; reliable under both `qs -p` and `qs -c`).
    readonly property string shellDir: Quickshell.env("RYOKU_SHELL_DIR")
    readonly property string script: (shellDir && shellDir.length > 0)
        ? shellDir + "/quickshell/shell/modules/wallpaper/switcher/index.sh"
        : (Quickshell.env("XDG_CONFIG_HOME") || (Quickshell.env("HOME") + "/.config")) + "/quickshell/shell/modules/wallpaper/switcher/index.sh"
    readonly property string stateDir: (Quickshell.env("XDG_STATE_HOME") || (Quickshell.env("HOME") + "/.local/state"))
    readonly property string statePath: root.stateDir + "/ryoku-wallpaper"
    readonly property string jsonPath: root.stateDir + "/ryoku-wallpaper.json"

    // per-output wallpaper state written by the daemon: { default, outputs }.
    // the legacy plain-path file is the fallback for state that predates the map.
    property var outputs: ({})
    property string defaultWall: ""
    readonly property string current: root.defaultWall
    function currentFor(name) {
        return (name && root.outputs[name]) ? root.outputs[name] : root.defaultWall;
    }
    function loadState() {
        var d = "";
        try {
            var o = JSON.parse(jsonView.text() || "{}");
            root.outputs = (o && o.outputs) ? o.outputs : ({});
            if (o && typeof o.default === "string")
                d = o.default;
        } catch (e) {
            root.outputs = ({});
        }
        root.defaultWall = d.length > 0 ? d : legacyView.text().trim();
    }
    FileView {
        id: jsonView
        path: root.jsonPath
        blockLoading: true
        watchChanges: true
        printErrors: false
        onFileChanged: reload()
        onLoaded: root.loadState()
        onLoadFailed: root.loadState()
    }
    FileView {
        id: legacyView
        path: root.statePath
        blockLoading: true
        watchChanges: true
        printErrors: false
        onFileChanged: reload()
        onLoaded: root.loadState()
    }

    function refresh() {
        if (indexProc.running)
            return;
        loading = true;
        indexProc.running = true;
    }

    Process {
        id: indexProc
        command: ["sh", root.script]
        stdout: StdioCollector {
            onStreamFinished: {
                var out = [];
                var lines = this.text.split("\n");
                for (var i = 0; i < lines.length; i++) {
                    var p = lines[i].split("\t");
                    if (p.length < 6)
                        continue;
                    var hue = parseFloat(p[4]) || 0;
                    var sat = parseFloat(p[5]) || 0;
                    var path = p[2];
                    out.push({
                        type: p[0],
                        mtime: parseFloat(p[1]) || 0,
                        path: path,
                        name: path.substring(path.lastIndexOf("/") + 1),
                        thumb: p[3],
                        preview: p.length > 6 ? p[6] : "",
                        hue: hue,
                        sat: sat,
                        group: Colors.bucket(hue, sat)
                    });
                }
                out.sort(function (a, b) {
                    var ga = a.group === Colors.neutral ? 100 : a.group;
                    var gb = b.group === Colors.neutral ? 100 : b.group;
                    if (ga !== gb)
                        return ga - gb;
                    return b.sat - a.sat;
                });
                root.entries = out;
                root.loading = false;
            }
        }
    }

    // apply the pick to a screen ("" / "*" = all outputs); a pick landing while
    // one is in flight queues and replays on exit, so rapid picks converge.
    property string queuedApply: ""
    property string queuedScreen: ""
    function setCmd(path, screen) {
        var c = ["ryogami", "wallpaper", "set", path];
        if (screen && screen !== "*")
            c = c.concat(["--screen", screen]);
        return c;
    }
    function apply(path, screen) {
        if (applyProc.running) {
            root.queuedApply = path;
            root.queuedScreen = screen || "";
            return;
        }
        applyProc.command = root.setCmd(path, screen || "");
        applyProc.running = true;
    }
    Process {
        id: applyProc
        onExited: {
            if (root.queuedApply.length) {
                var next = root.queuedApply, ns = root.queuedScreen;
                root.queuedApply = "";
                root.queuedScreen = "";
                applyProc.command = root.setCmd(next, ns);
                applyProc.running = true;
            }
        }
    }

    Component.onCompleted: refresh()
}
